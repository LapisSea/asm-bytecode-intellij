plugins {
	id 'org.jetbrains.intellij' version '1.16.0'
	id "com.github.johnrengelman.shadow" version "7.1.0"
	id 'java'
}

group 'asm.bytecode_outline.re'
version '0.4.4'

repositories {
	mavenCentral()
}

//setup a config so gradle prioritises our version of ASM
configurations {
	priority
	sourceSets.main.compileClasspath = configurations.priority + sourceSets.main.compileClasspath
	sourceSets.main.runtimeClasspath = configurations.priority + sourceSets.main.runtimeClasspath
}

dependencies {

	testImplementation 'org.junit.jupiter:junit-jupiter-api:'+junit_jupiter_version
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:'+junit_jupiter_version

	implementation group: 'org.ow2.asm', name: 'asm', version: asm_version

	priority "org.ow2.asm:asm:$asm_version"
	priority "org.ow2.asm:asm-commons:$asm_version"
	priority "org.ow2.asm:asm-util:$asm_version"

	//duplicated here so that IJ plugin puts them in the zip distribution
	runtimeOnly "org.ow2.asm:asm:$asm_version"
	runtimeOnly "org.ow2.asm:asm-commons:$asm_version"
	runtimeOnly "org.ow2.asm:asm-util:$asm_version"
}

shadowJar {
	configurations = [project.configurations.priority]
	relocate ('org.objectweb.asm', 'com.thiakil.shadowed.asm') {
		exclude 'org.objectweb.asm.idea.**'
	}
	archiveClassifier.set('')
}

jar {
	finalizedBy(tasks.shadowJar)
}

prepareSandbox.dependsOn(shadowJar)

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
	version = '2024.1'
	plugins = ['java']
}
test {
	useJUnitPlatform()
}
